apply plugin: 'groovy'
apply plugin: "com.gradle.plugin-publish"

group 'org.arquillian.spacelift.gradle'
version '1.0.0-alpha-18-SNAPSHOT'

allprojects {
    tasks.withType(JavaCompile) { 
        sourceCompatibility = "1.6"
        targetCompatibility = "1.6" 
    }
    tasks.withType(GroovyCompile) { 
        sourceCompatibility = "1.6"
        targetCompatibility = "1.6" 
    }
}

repositories {
    mavenCentral()
    // adding staging repository, might be handy for lastest Arquillian Spacelift version
    maven {
        name 'jboss-staging-repository-group'
        url 'https://repository.jboss.org/nexus/content/groups/staging'
    }
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        // publishing to Gradle repository
        classpath 'com.gradle.publish:plugin-publish-plugin:0.15.0'
  }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    // commons lang is used to do platform checks
    compile 'org.apache.commons:commons-lang3:3.1'

    // Spacelift abstracts execution of external commands
    compile 'org.arquillian.spacelift:arquillian-spacelift:1.0.0.Alpha7'
    compile 'org.jboss.aerogear.test:spacelift-jboss-manager:0.9.0'

    // FIXME manually include ShrinkWrap Resolver Depchain as pom typed import does not play well with Gradle and Eclipse
    compile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-api:2.1.1'
    compile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-spi:2.1.1'
    compile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-api-maven:2.1.1'
    compile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-spi-maven:2.1.1'
    compile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-maven:2.1.1'
    compile('org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-maven-archive:2.1.1') {
        // FIXME Sisu Plexus depends on Guava 10 while Gradle API requires Guava 11
        exclude group: "com.google.guava", module: "guava"
    }
    
    testCompile 'junit:junit:4.11'
}

test {
    testLogging {
        exceptionFormat "full"
    }
}

configurations {
    // exclude pom typed import of ShrinkWrap Resolver Depchain
    compile.exclude(module: "shrinkwrap-resolver-depchain")
}

// publish plugin to Gradle repository
pluginBundle {
  website = 'http://www.arquillian.org/'
  vcsUrl = 'https://github.com/arquillian/arquillian-spacelift-gradle'
  description = 'Gradle Plugin for Arquillian Spacelift'
  tags = ['test', 'automation', 'orchestration', 'process', 'arquillian', 'native']

  plugins {
    spaceliftPlugin {
      id = 'org.arquillian.spacelift'
      displayName = 'Gradle Plugin for Arquillian Spacelift'
    }
  }
}   

// construct package with sources
task sourcesJar(type: Jar, dependsOn: compileGroovy) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}
